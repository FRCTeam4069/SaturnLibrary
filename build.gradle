/*
 * Copyright 2019 Lo-Ellen Robotics
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.60" apply false
    id "org.jetbrains.dokka" version "0.10.0" apply false
    id "edu.wpi.first.GradleRIO" version "2019.4.1" apply false
    id "maven"
    id "maven-publish"
}

subprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.dokka"
    apply plugin: "edu.wpi.first.GradleRIO"
    apply plugin: "maven"
    apply plugin: "maven-publish"

    group = "frc.team4069.saturn.lib"
    version = "2020.0.0"

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    tasks.dokka {
        outputFormat = "html"
        outputDirectory = "$buildDir/javadoc"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = ["-Xjvm-default=compatibility"]
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                afterEvaluate {
                    artifactId = jar.archiveBaseName.get()
                }
            }
        }
    }
}

wrapper {
    gradleVersion = "6.0.1"
}
